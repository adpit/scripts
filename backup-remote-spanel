#!/usr/bin/perl -w

use strict;
use List::Util qw/shuffle/;
use Proc::PID::File;
use App::Options (
    option => {
        rsync_opts => {
            description => "Extra rsync options, will be passed to 'backup-with-history' script",
            type        => "string",
            default     => "",
            required    => 0,
        },
        bin_path => {
            description => "Path to 'backup-with-history' script",
            type        => "string",
            default     => "",
            required    => 0,
        },
        hist_levels => {
            description => "A comma-separated list of numbers to specify the history levels, will be passed to 'backup-with-history' script",
            type        => "string",
            default     => "-7,4,3",
            required    => 0,
        },
        user => {
            description => "User",
            type        => "string",
            required    => 1,
            default     => "root",
        },
        host => {
            description => "Host",
            type        => "string",
            required    => 1,
        },
        server_id => {
            description => "Server ID",
            type        => "string",
            required    => 1,
        },
        ssh_port => {
            description => "ssh port",
            type        => "int",
            required    => 1,
            default     => 22,
        },
        random => {
            description => "randomize order of directories, etc",
            type        => "boolean",
            required    => 1,
            default     => 1,
        },
    }
);
use Log::Log4perl qw(:easy);

# --- subs

sub esc {
    local $_ = shift;
    s/'/'"'"'/g;
    "'$_'";
}

# --- main

@ARGV == 1 or die "Usage: $0 <dest>\n";
my ( $dst0 ) = @ARGV;
for ( $dst0 ) {s!/$!!}

Log::Log4perl->easy_init($DEBUG);

my $name = $App::options{server_id};
$name =~ s/[^A-Za-z0-9]+/_/g;
LOGDIE "Remote backup to Spanel server $App::options{server_id} already running, aborting"
    if Proc::PID::File->running(name=>$name);

INFO "Remote backup for Spanel server $App::options{server_id} ($App::options{host}) started";




INFO "Listing backup directories to backup ...";
my $sshopts = "-p $App::options{ssh_port} -c arcfour -o StrictHostKeyChecking=no";
my $dir0 = "/backup/$App::options{server_id}.local";
my $cmd = "ssh $sshopts " .
    "$App::options{user}\@$App::options{host} \"'ls'\" -1 $dir0/";
DEBUG "cmd: $cmd";
my @dirs = `$cmd`;
@dirs or do {
    WARN "There is no subdirs under $dir0/";
    exit 1;
};
chomp for @dirs;
@dirs = shuffle @dirs if $App::options{random};

my $i = 0;
for my $dir (@dirs) {
    $i++;

    my $dst = $dir;
    $dst =~ s!/+!-!g;
    $dst =~ s!^-!!g;
    $dst =~ s!-$!!g;
    if ($dst eq '.') { $dst = "CURDIR" }
    elsif ($dst eq '..') { $dst = "PARENT" }

    my $bin = ($App::options{bin_path} ? "$App::options{bin_path}/" : "") . "backup-with-history";
    my $cmd = "$bin " .
        ($App::options{hist_levels} ? "--hist_levels=".esc($App::options{hist_levels})." " : "").
        ($App::options{rsync_opts} ? "--rsync_opts=".esc($App::options{rsync_opts})." " : "").
        esc("$App::options{user}\@$App::options{host}:$dir0/$dir/current") . " ".
        esc("$dst0/$dst");
    INFO "($i/".scalar(@dirs).") Running backup for dir=$dir/current ...";
    DEBUG "cmd: $cmd";
    system $cmd;
}

INFO "Remote backup for Spanel server $App::options{server_id} ($App::options{host}) ended";
