#!/usr/bin/env perl

use 5.010;
use autodie;
use Getopt::Long;
use String::ShellQuote;

my %Opts = (
    repos_path => '/cpan',
);

my $res = GetOptions(
    'repos-path|p=s' => \$Opts{repos_path},
);

die "Usage: $0 <module> ...\n" unless @ARGV;

my $num_found = 0;
open my($fh), "zcat ".shell_quote("$Opts{repos_path}/modules/02packages.details.txt.gz")."|";
while (<$fh>) {
    next unless /\S/;
    next if /^\S+:\s/;
    chomp;
    my ($pkg, $ver, $path) = split /\s+/, $_;
    for my $mod (@ARGV) {
        next unless $pkg eq $mod;
        $num_found++;
        say "$Opts{repos_path}/authors/id/$path";
    }
    last if $num_found >= @ARGV;
}

exit $num_found >= @ARGV ? 0 : 1;
