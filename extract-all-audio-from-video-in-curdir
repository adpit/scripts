#!/usr/bin/env perl

BEGIN { $ENV{TRACE} = 1 }

use warnings;
use strict;
use Log::Any::App qw($log);

use Builtin::Logged qw(system);
use Cwd;
use File::Which;
use Getopt::Long;
use String::ShellQuote;

# --- globals

my %DefOpt = (
    log_level => 'info',
    bitrate => '128k',
    samprate => '44100',
    copy => 0,
    format => 'mp3',
);
my %Opt = %DefOpt;

GetOptions(
    'bitrate=s' => \$Opt{bitrate},
    'samprate=i' => \$Opt{samprate},
    'help' => \$Opt{help},
    'copy!' => \$Opt{copy},
    'format=s' => \$Opt{format},
);
if ($Opt{help}) {
    print <<EOF;
$0 [options] [FILES ...]

If files are not specified, process all movie files in current dir.

Options:
 --copy         Save time by just copying the stream instead of re-encoding. To
                use this successfully, you must be sure that your input audio
                stream is already MP3.
 --bitrate      Set bitrate (default: $DefOpt{bitrate}).
 --samprate     Set sampling rate (default: $DefOpt{samprate}).
 --format F     Set output format (default: $DefOpt{format}).
EOF
    exit 0;
}

my $bin = which("avconv") || which("ffmpeg"); die "Can't find avconv/ffmpeg" unless $bin;
my $cwd = getcwd;
my @files;
if (@ARGV) { @files = @ARGV } else { @files = (<*.ogg>, <*.avi>, <*.mpg>, <*.mpeg>, <*.rmvb>, <*.flv>, <*.mp4>, <*.mkv>, <*.webm>) }
for my $f (@files) {
    chdir $cwd;
    $log->info("Processing file '$f' ...");
    my $d = $f; $d =~ s/\.\w+$/\.$Opt{format}/;
    if (-e $d) {
        $log->warn("'$d' already exists, skipped");
        next;
    }
    my @opts = ();
    if ($f =~ /\.rmvb$/ || !$Opt{copy}) {
        push @opts, "-ab $Opt{bitrate} -ar $Opt{samprate}";
    } else {
        push @opts, "-acodec copy";
    }
    system $bin." -i ".shell_quote($f)." ".join(" ", @opts)." ".shell_quote($d);
    if ($?) {
        $log->error("Failed extracting audio from '$f': $! ($?), skipped");
        next;
    }
}
