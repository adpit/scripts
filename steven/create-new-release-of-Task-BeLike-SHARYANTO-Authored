#!/usr/bin/perl

# This is a one-liner for creating a new release of
# Task::BeLike::SHARYANTO::Authored and publishing it.

use 5.010;
use autodie;
use strict;
use warnings;

use DateTime;
use File::chdir;
use File::Slurp;

$CWD = "$ENV{HOME}/repos/perl-Task-BeLike-SHARYANTO-Authored";

my $fn = "dist.ini";
my $f_d = read_file($fn);
my ($curver) = $f_d =~ /^\s*version\s*=\s*(.+)/m;
my $nextver = $curver + 0.01;

my $date = DateTime->now->ymd;

my $res = `git status`;
unless ($res =~ /working directory clean/) {
    # so if things get whacky, we can reset using 'checkout'
    die "Repo is not in clean state";
}

$f_d =~ s/^(\s*version\s*)=(.+)/$1=$nextver/m
    or die "Can't replace version in dist.ini";
write_file($fn, $f_d);

$fn = "Changes";
my $f_c = read_file($fn);
$f_c =~ s/^(\Q$curver\E )/$nextver  $date (generated by $0)\n\n  - Update\n\n$1/m
    or die "Can't insert Changes entry";
write_file($fn, $f_c);

my $pkgs = `generate-pkg-lists-for-tbsha`;

$fn = "lib/Task/BeLike/SHARYANTO/Authored.pm";
my $f_pm = read_file($fn);
$f_pm =~ s/(^=pkgroup [^\n]+\n\n).+(^=cut)/$1$pkgs$2/sm
    or die "Can't insert pkg lists entry";
write_file($fn, $f_pm);

system "release-cpan-module";
