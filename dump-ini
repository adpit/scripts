#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{dump_ini} = {
    v => 1.1,
    summary => 'Parse INI using a parser and display the result',
    args => {
        ini => {
            summary => "Input INI file",
            schema => 'str*',
            req => 1,
            pos => 0,
            cmdline_src => 'stdin_or_files',
        },
        parser => {
            summary => "Choose a parser",
            schema => ['str*', in=>[qw/Config::INI::Reader reader
                                       Config::IniFiles inifiles
                                      /]],
            default => 'Config::INI::Reader',
        },
    },
    result_naked => 1,
};
sub dump_ini {
    my %args = @_;

    my $parser = $args{parser};

    if ($parser eq 'Config::INI::Reader' || $parser eq 'reader') {
        require Config::INI::Reader;
        return Config::INI::Reader->read_string($args{ini});
    } elsif ($parser eq 'Config::IniFiles' || $parser eq 'inifiles') {
        require Config::IniFiles;
        no warnings 'once';
        my $cfg = Config::IniFiles->new(-file => \$args{ini})
            or die join("", @Config::IniFiles::errors);
        return $cfg->{v};
    } else {
        die "Unknown parser";
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/dump_ini',
)->run;
