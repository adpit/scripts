#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;

use Desktop::Detect qw(detect_desktop);
use File::Slurp::Tiny qw(read_file write_file);

unless (@ARGV == 1) {
    die "Usage: $0 <minutes>\n";
}
my $mins = $ARGV[0] + 0; $mins = 1 if $mins < 1;

my $detres = detect_desktop();

if ($detres->{desktop} eq 'kde') {
    my $path = "$ENV{HOME}/.kde/share/config/kscreensaverrc";
    my $ct = read_file($path);
    my $secs = $mins*60;
    $ct =~ s/^(Timeout\s*=\s*)(\S+)/${1}$secs/m
        or die "Can't subtitute Timeout value in $path\n";
    write_file($path, $ct);
} elsif ($detres->{desktop} eq 'xfce') { # XXX or has xscreensaver process running
    my $path = "$ENV{HOME}/.xscreensaver";
    my $ct = read_file($path);
    my $hours = int($mins/60);
    $mins -= $hours*60;

    $ct =~ s/^(timeout:\s*)(\S+)/sprintf("%s%d:%02d:%02d",$1,$hours,$mins,0)/em
        or die "Can't subtitute timeout value in $path\n";
    write_file($path, $ct);
    system "killall", "-HUP", "xscreensaver";
    $? == 0 or die "Can't HUP xscreensaver\n";
} else {
    die "Desktop not supported\n";
}
